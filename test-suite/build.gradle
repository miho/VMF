import java.text.SimpleDateFormat

buildscript {

    ext.commonProps = new Properties()
    file(new File(projectDir.parentFile,"config/common.properties")).withInputStream { commonProps.load(it) }

    repositories {
        mavenCentral()
        jcenter()
        mavenLocal()
    }

    dependencies {
        classpath "eu.mihosoft.vmf:vmf-gradle-plugin:0.2.7.6"
    }
}

plugins {
    id 'com.github.hierynomus.license' version '0.14.0'
//    id 'eu.mihosoft.vmf' version '0.2-SNAPSHOT'
}

ext.vmfPluginIntelliJIntegration = true

apply plugin: 'java'
apply plugin: 'eu.mihosoft.vmf'

idea {
    module {
        dependencies {
            compile group: 'eu.mihosoft.vmf', name: 'vmf', version: '0.2.7.6'
        }
    }
}

sourceCompatibility = '1.8'
targetCompatibility = '1.8'
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

tasks.withType(Javadoc) {
    options.addStringOption('Xdoclint:none', '-quiet')
    options.encoding = 'UTF-8'
}

if (!hasProperty('mainClass')) {
    ext.mainClass = 'eu.mihosoft.vmf.tutorial01.Main'
}

wrapper {
    gradleVersion = '6.0'
}

repositories {
    mavenCentral()
    jcenter()
    mavenLocal()
}

dependencies {

    testCompile('junit:junit:4.12'){
        exclude group: 'org.hamcrest'
    }

    testCompile 'org.hamcrest:hamcrest-library:1.3'

    testCompile group: 'eu.mihosoft.vmf', name: 'vmf', version: '0.2.7.6'
    compile group: 'eu.mihosoft.vmf', name: 'vmf-runtime', version: '0.2.7.6'
    compile group: 'org.mdkt.compiler', name: 'InMemoryJavaCompiler', version: '1.3.0'
    compile group: 'org.codehaus.groovy', name: 'groovy-console', version: '2.0.1'
    
    compile 'commons-jxpath:commons-jxpath:1.3'
}

task run(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    main = mainClass
}

license {
    header = rootProject.file('config/HEADER')
    strictCheck = true
    ignoreFailures = true
    mapping {
        java   = 'SLASHSTAR_STYLE'
        groovy = 'SLASHSTAR_STYLE'
        fxml   = 'XML_STYLE'
    }

    ext.yearCurrent  = new SimpleDateFormat("yyyy").format(new Date())
    ext.yearSince1   = '2017'
    ext.author1 = 'Michael Hoffer <info@michaelhoffer.de>'
    ext.yearSince2   = '2017'
    ext.author2= 'Goethe Center for Scientific Computing, University Frankfurt'
    ext.yearSince3   = '2017'
    ext.author3 = 'Samuel Michaelis'

    exclude '**/*.svg'
}


